use master
if exists (select * from sysdatabases where name = 'VOVBACSI')
    drop database VOVBACSI
go
create database VOVBACSI
go
use VOVBACSI
go
CREATE TABLE [PHANQUYEN] (
    [MaPQ] INTEGER NOT NULL IDENTITY,
    [TenPQ] NVARCHAR(50) NOT NULL,
    PRIMARY KEY([MaPQ])
);
GO

CREATE TABLE [NGUOIDUNG] (
    [MaND] INTEGER NOT NULL IDENTITY,
    [TenND] NVARCHAR(50),
    [Password] VARCHAR(50) NOT NULL,
    [Email] VARCHAR(50),
    [sdt] VARCHAR(15) NOT NULL,
    [DiaChi] NVARCHAR(100),
    [NgaySinh] DATE,
    [GioiTinh] NVARCHAR(5),
    [AnhCaNhan] VARCHAR(50),
    [SoDuTK] INTEGER NOT NULL,
    [MaGioiThieu] INTEGER NOT NULL,
    [MaNgGioiThieu] INTEGER,
    [MaPQ] INTEGER NOT NULL,
    PRIMARY KEY([MaND])
);
GO

CREATE TABLE [KHOABENH] (
    [MaKB] INTEGER NOT NULL IDENTITY,
    [TenKB] NVARCHAR(50) NOT NULL,
    [MoTa] NVARCHAR(500),
    PRIMARY KEY([MaKB])
);
GO
CREATE TABLE [BENHVIEN] (
    [MaBenhVien] INTEGER NOT NULL IDENTITY,
    [TenBenhVien] NVARCHAR(150) NOT NULL,
    [HinhAnh] VARCHAR(150) NOT NULL,
    PRIMARY KEY([MaBenhVien])
);
GO

CREATE TABLE [TRANGTHAIBACSI] (
    [MaTTBS] INTEGER NOT NULL IDENTITY UNIQUE,
    [TênTTBS] NVARCHAR,
    PRIMARY KEY([MaTTBS])
);
GO

CREATE TABLE [BACSI] (
    [MaBS] INTEGER NOT NULL IDENTITY,
    [SoCCCD] VARCHAR(20) NOT NULL,
    [NgayCapCCCD] DATETIME NOT NULL,
    [NoiCapCCCD] NVARCHAR(50) NOT NULL,
    [AnhCCCDTruoc] VARCHAR(150) NOT NULL,
    [AnhCCCDSau] VARCHAR(150) NOT NULL,
    [ChuyenKhoa] NVARCHAR(150) NOT NULL,
    [NamKN] INTEGER NOT NULL,
    [MaBenhVien] INTEGER NOT NULL,
    [MaKB] INTEGER NOT NULL,
    [ChungChiHN] NVARCHAR(150) NOT NULL,
    [NgayCapCC] DATE NOT NULL,
    [AnhCCTruoc] VARCHAR(150) NOT NULL,
    [AnhCCSau] VARCHAR(150) NOT NULL,
    [HocVi] NVARCHAR(50),
    [HocHam] NVARCHAR(50),
    [ChuyenMon] NVARCHAR(50),
    [ThongTinBangCap] NVARCHAR(max),
    [GioiThieu] NVARCHAR(max),
    [CacBenhDieuTri] NVARCHAR(max),
    [QuaTrinhHoc] NVARCHAR(max),
    [QuaTrinhCongTac] NVARCHAR(max),
    [NghienCuuKH] NVARCHAR(max),
    [GiangDay] NVARCHAR(max),
    [HoiVien] NVARCHAR(max),
    [SoTienKham] INTEGER NOT NULL,
    [MaND] INTEGER NOT NULL,
    [MaTTBS] INTEGER NOT NULL,
    PRIMARY KEY([MaBS])
);
GO

CREATE TABLE [CHUYENNGANH] (
    [MaCN] INTEGER NOT NULL IDENTITY,
    [TenCN] NVARCHAR(100) NOT NULL,
    [HinhAnh] VARCHAR(150) NOT NULL,
    PRIMARY KEY([MaCN])
);
GO

CREATE TABLE [LOAIBAIVIET] (
    [MaLBV] INTEGER NOT NULL IDENTITY,
    [TenLBV] NVARCHAR(100) NOT NULL,
    PRIMARY KEY([MaLBV])
);
GO

CREATE TABLE [BAIVIET] (
    [MaBV] INTEGER NOT NULL IDENTITY,
    [TieuDeBV] NVARCHAR(100) NOT NULL,
    [NoiDung] NVARCHAR(max) NOT NULL,
    [MaKB] INTEGER,
    [LinkYtb] VARCHAR(100),
    [NgayDang] DATE NOT NULL,
    [MaLBV] INTEGER NOT NULL,
    [LuotXem] INTEGER,
    PRIMARY KEY([MaBV])
);
GO

CREATE TABLE [HINHANHBENH] (
    [MaHinh] INTEGER NOT NULL IDENTITY,
    [MaHS] INTEGER NOT NULL,
    [HinhAnhBenh] VARCHAR(100) NOT NULL,
    PRIMARY KEY([MaHinh])
);
GO

CREATE TABLE [HOSO] (
    [MaHS] INTEGER NOT NULL IDENTITY,
    [MaND] INTEGER NOT NULL,
    [MoTaBenh] NVARCHAR(max),
    PRIMARY KEY([MaHS])
);
GO

CREATE TABLE [TRANGTHAICUOCHEN] (
    [MaTTCH] INTEGER NOT NULL IDENTITY,
    [TenTTCH] NVARCHAR(50) NOT NULL,
    [MoTaTTCH] NVARCHAR(200),
    PRIMARY KEY([MaTTCH])
);
GO

CREATE TABLE [CUOCHENKHAM] (
    [MaCHK] INTEGER NOT NULL IDENTITY,
    [MaND] INTEGER NOT NULL,
    [MaBS] INTEGER,
    [MaHS] INTEGER,
    [MaTTCH] INTEGER NOT NULL,
    [ThoiGianHen] DATETIME,
    [ThoiGianBD] DATETIME,
    [ThoiGianKT] DATETIME,
    [SoTT] TINYINT,
    [GhiChuBS] NVARCHAR(200),
    [Video] VARCHAR(200),
    [SoTienTT] INTEGER,
    [MaKM] INTEGER,
    PRIMARY KEY([MaCHK])
);
GO

CREATE TABLE [KHUYENMAI] (
    [MaKM] INTEGER NOT NULL IDENTITY,
    [TenKM] NVARCHAR(100),
    [MoTaKM] NVARCHAR(200),
    [PhanTramGia] INTEGER,
    [GiamToiDa] INTEGER,
    [NgayBDKM] DATETIME,
    [NgayKTKM] DATETIME,
    PRIMARY KEY([MaKM])
);
GO

CREATE TABLE [DANHGIA] (
    [MaDG] INTEGER NOT NULL IDENTITY,
    [MaCHK] INTEGER NOT NULL,
    [NoiDung] NVARCHAR(200),
    [NgayDG] DATE NOT NULL,
    [SoSao] TINYINT NOT NULL,
    PRIMARY KEY([MaDG])
);
GO

CREATE TABLE [THANHTOAN] (
    [MaTT] INTEGER NOT NULL IDENTITY,
    [MaND] INTEGER NOT NULL,
    [SoTien] INTEGER NOT NULL,
    [NgayTT] DATE NOT NULL,
    [MaCHK] INTEGER NOT NULL,
    PRIMARY KEY([MaTT])
);
GO

CREATE TABLE [THONGBAO] (
    [MaTB] INTEGER NOT NULL IDENTITY,
    [TieuDeTB] NVARCHAR(100),
    [NoiDungTB] NVARCHAR(500),
    [ThoiGianTB] DATETIME,
    [TrangThaiDoc] BIT,
    PRIMARY KEY([MaTB])
);
GO

CREATE TABLE [HOATDONG] (
    [MaHD] INTEGER NOT NULL IDENTITY,
    [NoiDungHD] NVARCHAR(100),
    PRIMARY KEY([MaHD])
);
GO

CREATE TABLE [LICHSUHD] (
    [MaLS] INTEGER NOT NULL IDENTITY,
    [MaHD] INTEGER,
    [ThoiGianHD] TIME,
    PRIMARY KEY([MaLS])
);
GO

CREATE TABLE [BACSI_CHUYENNGANH] (
    [MaBS] INTEGER NOT NULL,
    [MaCN] INTEGER NOT NULL,
    PRIMARY KEY([MaBS], [MaCN])
);
GO

CREATE TABLE [KHUYENMAI_CHUYENNGANH] (
    [MaCN] INTEGER NOT NULL,
    [MaKM] INTEGER NOT NULL,
    PRIMARY KEY([MaCN], [MaKM])
);
GO

CREATE TABLE [THONGBAO_NGUOIDUNG] (
    [MaTB] INTEGER NOT NULL,
    [MaND] INTEGER NOT NULL,
    PRIMARY KEY([MaTB], [MaND])
);
GO

CREATE TABLE [NGUOIDUNG_LICHSUHD] (
    [MaND] INTEGER NOT NULL,
    [MaLS] INTEGER NOT NULL,
    PRIMARY KEY([MaND], [MaLS])
);
GO

-- Khóa ngoại được điều chỉnh, tránh tạo khóa ngoại trên chính bảng chứa khóa ngoại

ALTER TABLE [NGUOIDUNG] 
ADD CONSTRAINT FK_NGUOIDUNG_PHANQUYEN FOREIGN KEY([MaPQ]) REFERENCES [PHANQUYEN]([MaPQ]);

ALTER TABLE [BACSI] 
ADD CONSTRAINT FK_BACSI_BENHVIEN FOREIGN KEY([MaBenhVien]) REFERENCES [BENHVIEN]([MaBenhVien]);

ALTER TABLE [BACSI] 
ADD CONSTRAINT FK_BACSI_KHOABENH FOREIGN KEY([MaKB]) REFERENCES [KHOABENH]([MaKB]);

ALTER TABLE [BACSI] 
ADD CONSTRAINT FK_BACSI_TRANGTHAIBACSI FOREIGN KEY([MaTTBS]) REFERENCES [TRANGTHAIBACSI]([MaTTBS]);

ALTER TABLE [BAIVIET] 
ADD CONSTRAINT FK_BAIVIET_LOAIBAIVIET FOREIGN KEY([MaLBV]) REFERENCES [LOAIBAIVIET]([MaLBV]);

ALTER TABLE [BAIVIET] 
ADD CONSTRAINT FK_BAIVIET_KHOABENH FOREIGN KEY([MaKB]) REFERENCES [KHOABENH]([MaKB]);

ALTER TABLE [CUOCHENKHAM] 
ADD CONSTRAINT FK_CUOCHENKHAM_NGUOIDUNG FOREIGN KEY([MaND]) REFERENCES [NGUOIDUNG]([MaND]);

ALTER TABLE [CUOCHENKHAM] 
ADD CONSTRAINT FK_CUOCHENKHAM_BACSI FOREIGN KEY([MaBS]) REFERENCES [BACSI]([MaBS]);

ALTER TABLE [CUOCHENKHAM] 
ADD CONSTRAINT FK_CUOCHENKHAM_HOSO FOREIGN KEY([MaHS]) REFERENCES [HOSO]([MaHS]);

ALTER TABLE [CUOCHENKHAM] 
ADD CONSTRAINT FK_CUOCHENKHAM_TRANGTHAICUOCHEN FOREIGN KEY([MaTTCH]) REFERENCES [TRANGTHAICUOCHEN]([MaTTCH]);

ALTER TABLE [HOSO] 
ADD CONSTRAINT FK_HOSO_NGUOIDUNG FOREIGN KEY([MaND]) REFERENCES [NGUOIDUNG]([MaND]);

ALTER TABLE [HINHANHBENH] 
ADD CONSTRAINT FK_HINHANHBENH_HOSO FOREIGN KEY([MaHS]) REFERENCES [HOSO]([MaHS]);

ALTER TABLE [DANHGIA] 
ADD CONSTRAINT FK_DANHGIA_CUOCHENKHAM FOREIGN KEY([MaCHK]) REFERENCES [CUOCHENKHAM]([MaCHK]);

ALTER TABLE [THANHTOAN] 
ADD CONSTRAINT FK_THANHTOAN_NGUOIDUNG FOREIGN KEY([MaND]) REFERENCES [NGUOIDUNG]([MaND]);

ALTER TABLE [THANHTOAN] 
ADD CONSTRAINT FK_THANHTOAN_CUOCHENKHAM FOREIGN KEY([MaCHK]) REFERENCES [CUOCHENKHAM]([MaCHK]);

ALTER TABLE [BACSI_CHUYENNGANH] 
ADD CONSTRAINT FK_BACSI_CHUYENNGANH_BACSI FOREIGN KEY([MaBS]) REFERENCES [BACSI]([MaBS]);

ALTER TABLE [BACSI_CHUYENNGANH] 
ADD CONSTRAINT FK_BACSI_CHUYENNGANH_CHUYENNGANH FOREIGN KEY([MaCN]) REFERENCES [CHUYENNGANH]([MaCN]);

ALTER TABLE [KHUYENMAI_CHUYENNGANH] 
ADD CONSTRAINT FK_KHUYENMAI_CHUYENNGANH_KHUYENMAI FOREIGN KEY([MaKM]) REFERENCES [KHUYENMAI]([MaKM]);

ALTER TABLE [KHUYENMAI_CHUYENNGANH] 
ADD CONSTRAINT FK_KHUYENMAI_CHUYENNGANH_CHUYENNGANH FOREIGN KEY([MaCN]) REFERENCES [CHUYENNGANH]([MaCN]);

ALTER TABLE [THONGBAO_NGUOIDUNG] 
ADD CONSTRAINT FK_THONGBAO_NGUOIDUNG_THONGBAO FOREIGN KEY([MaTB]) REFERENCES [THONGBAO]([MaTB]);

ALTER TABLE [THONGBAO_NGUOIDUNG] 
ADD CONSTRAINT FK_THONGBAO_NGUOIDUNG_NGUOIDUNG FOREIGN KEY([MaND]) REFERENCES [NGUOIDUNG]([MaND]);

ALTER TABLE [NGUOIDUNG_LICHSUHD] 
ADD CONSTRAINT FK_NGUOIDUNG_LICHSUHD_NGUOIDUNG FOREIGN KEY([MaND]) REFERENCES [NGUOIDUNG]([MaND]);

ALTER TABLE [NGUOIDUNG_LICHSUHD] 
ADD CONSTRAINT FK_NGUOIDUNG_LICHSUHD_LICHSUHD FOREIGN KEY([MaLS]) REFERENCES [LICHSUHD]([MaLS]);

-- Bổ sung cột hoàn phí vào bảng THANHTOAN nếu chưa có
IF COL_LENGTH('THANHTOAN', 'IsRefunded') IS NULL
    ALTER TABLE THANHTOAN ADD IsRefunded BIT NOT NULL DEFAULT 0;
IF COL_LENGTH('THANHTOAN', 'RefundReason') IS NULL
    ALTER TABLE THANHTOAN ADD RefundReason NVARCHAR(200) NULL;
IF COL_LENGTH('THANHTOAN', 'RefundDate') IS NULL
    ALTER TABLE THANHTOAN ADD RefundDate DATETIME NULL;
GO

-- Xóa procedure cũ nếu có
IF OBJECT_ID('HoanPhiKham', 'P') IS NOT NULL
    DROP PROCEDURE HoanPhiKham;
GO

-- Tạo lại procedure hoàn phí khám
CREATE PROCEDURE HoanPhiKham
    @MaTT INT,
    @RefundReason NVARCHAR(200) = NULL
AS
BEGIN
    SET NOCOUNT ON;
    DECLARE @MaND INT, @SoTien INT, @IsRefunded BIT;
    SELECT @MaND = MaND, @SoTien = SoTien, @IsRefunded = IsRefunded FROM THANHTOAN WHERE MaTT = @MaTT;
    IF @IsRefunded = 1
    BEGIN
        RAISERROR(N'Giao dịch đã hoàn phí trước đó!', 16, 1);
        RETURN;
    END
    -- Cộng lại tiền cho user
    UPDATE NGUOIDUNG SET SoDuTK = SoDuTK + @SoTien WHERE MaND = @MaND;
    -- Đánh dấu đã hoàn phí, ghi lý do và ngày
    UPDATE THANHTOAN SET IsRefunded = 1, RefundReason = @RefundReason, RefundDate = GETDATE() WHERE MaTT = @MaTT;
END
GO

-- Xóa procedure cũ nếu có
IF OBJECT_ID('LayDanhSachThanhToan', 'P') IS NOT NULL
    DROP PROCEDURE LayDanhSachThanhToan;
GO

CREATE PROCEDURE LayDanhSachThanhToan 
AS 
BEGIN 
    SET NOCOUNT ON; 
    
    SELECT  
        tt.MaTT,                             -- Mã thanh toán 
        tt.NgayTT,                           -- Ngày thanh toán 
        tt.SoTien,                           -- Số tiền 
        nd.TenND,                            -- Tên người dùng 
        ck.ThoiGianHen AS ChiTraHenKhamLuc,  -- Thời gian hẹn khám 
        tt.RefundReason,                     -- Lý do hoàn phí 
        tt.RefundDate                        -- Ngày hoàn phí 
    FROM  
        THANHTOAN tt 
    JOIN  
        NGUOIDUNG nd ON tt.MaND = nd.MaND    -- Kết nối với bảng người dùng 
    LEFT JOIN  
        CUOCHENKHAM ck ON tt.MaCHK = ck.MaCHK  -- Kết nối với bảng cuộc hẹn khám 
END 
GO

-- Xóa procedure cũ nếu có
IF OBJECT_ID('UpdateSoDuTKForUserAndDoctor', 'P') IS NOT NULL
    DROP PROCEDURE UpdateSoDuTKForUserAndDoctor;
GO

CREATE PROCEDURE UpdateSoDuTKForUserAndDoctor 
    @MaND INT,   -- Mã người dùng của bác sĩ 
    @MaCHK INT    -- Mã cuộc hẹn cần cập nhật 
AS 
BEGIN 
    DECLARE @MaBS INT;           -- Mã bác sĩ 
    DECLARE @SoTienKham INT;     -- Số tiền khám của bác sĩ 
    DECLARE @SoDuTK_BN INT;      -- Số dư tài khoản của bệnh nhân 
    DECLARE @SoDuTK_BS INT;      -- Số dư tài khoản của bác sĩ 
    DECLARE @MaNDBN INT;         -- Mã người dùng của bệnh nhân

    -- Lấy mã bác sĩ từ bảng BACSI dựa trên MaND (bác sĩ) 
    SELECT TOP 1 @MaBS = MaBS 
    FROM BACSI 
    WHERE MaND = @MaND; 

    -- Lấy số tiền khám của bác sĩ 
    SELECT TOP 1 @SoTienKham = SoTienKham 
    FROM BACSI 
    WHERE MaBS = @MaBS; 

    -- Lấy mã người dùng của bệnh nhân từ cuộc hẹn
    SELECT @MaNDBN = MaND FROM CUOCHENKHAM WHERE MaCHK = @MaCHK;

    -- Lấy số dư tài khoản của bệnh nhân 
    SELECT @SoDuTK_BN = SoDuTK 
    FROM NGUOIDUNG 
    WHERE MaND = @MaNDBN; 

    -- Lấy số dư tài khoản của bác sĩ 
    SELECT @SoDuTK_BS = SoDuTK 
    FROM NGUOIDUNG 
    WHERE MaND = @MaND; 

    -- Cập nhật lại số dư tài khoản của bệnh nhân (trừ số tiền khám) 
    UPDATE NGUOIDUNG 
    SET SoDuTK = @SoDuTK_BN - @SoTienKham 
    WHERE MaND = @MaNDBN; 

    -- Cập nhật lại số dư tài khoản của bác sĩ (thêm số tiền khám * 70%) 
    UPDATE NGUOIDUNG 
    SET SoDuTK = @SoDuTK_BS + (@SoTienKham * 0.7) 
    WHERE MaND = @MaND; 

    -- Cập nhật lại số tiền thanh toán của cuộc hẹn đó 
    UPDATE CUOCHENKHAM 
    SET SoTienTT = @SoTienKham 
    WHERE MaCHK = @MaCHK; 

    -- Cập nhật lại số TT của cuộc hẹn khám nếu MaTTCH = 4 thì đặt lại là 0, còn lại trừ 1 
    UPDATE CUOCHENKHAM 
    SET SoTT = CASE 
                WHEN MaTTCH = 4 THEN 0 
                ELSE SoTT - 1 
               END 
    WHERE MaCHK = @MaCHK AND MaBS = @MaBS; 

    -- Thêm thông tin thanh toán vào bảng THANHTOAN nếu chưa có
    IF NOT EXISTS (SELECT 1 FROM THANHTOAN WHERE MaCHK = @MaCHK)
    BEGIN
        INSERT INTO THANHTOAN (MaND, SoTien, NgayTT, MaCHK)
        VALUES (@MaNDBN, @SoTienKham, GETDATE(), @MaCHK);
    END
END;
GO

